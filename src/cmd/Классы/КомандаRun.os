#Использовать logos

//    Copyright 2018 khorevaa
// 
//    Licensed under the Apache License, Version 2.0 (the "License");
//    you may not use this file except in compliance with the License.
//    You may obtain a copy of the License at
// 
//        http://www.apache.org/licenses/LICENSE-2.0
// 
//    Unless required by applicable law or agreed to in writing, software
//    distributed under the License is distributed on an "AS IS" BASIS,
//    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//    See the License for the specific language governing permissions and
//    limitations under the License.
Перем Лог;

Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("v verbose", Ложь, "вывод отладочной информация в процессе выполнении")
					.Флаговый()
					.ВОкружении("TASKS_VERBOSE");

	КомандаПриложения.Опция("f tasks-file", "", "файл с настройкой заданий");
	КомандаПриложения.Опция("W workspace", ТекущийКаталог(), "путь к каталогу рабочей области");
	КомандаПриложения.Опция("D work-dir", ТекущийКаталог(), "путь к рабочему каталогу");
	КомандаПриложения.Опция("s save", Ложь, "признак необходимости сохранить задачу");
	КомандаПриложения.Опция("g group", Ложь, "это группа задач");
	КомандаПриложения.Опция("l label", "", "представление задачи при сохранении");
	КомандаПриложения.Опция("name", "", "наименование задачи при сохранении");
	КомандаПриложения.Опция("redirect-out", Неопределено, "перехват потоков вывода и вывода задачи").ТБулево();
	КомандаПриложения.Опция("t type", "process", "тип запуска задачи")
					.ТПеречисление()
					.Перечисление("process", "process", "задача будет запускаться как отдельный процесс")
					.Перечисление("internal", "internal", "задача будет запускаться как загрузка скрипта в КомандаПриложения tasks")
					.Перечисление("cmd", "cmd", "задача будет запускаться c помощью отдельного cmd/shell")
					;
	
	КомандаПриложения.Опция("o option", "", "массив опций в виде ключ=значение").ТМассивСтрок();
	КомандаПриложения.Опция("e env", "", "массив переменных окружения в виде ключ=значение").ТМассивСтрок();
	
	КомандаПриложения.Аргумент("TASK", "", "имя задачи для выполнения");
	КомандаПриложения.Аргумент("ARGS", "", "аргументы для выполнения задачи")
						.ТМассивСтрок()
						.Обязательный(Ложь);
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт

	ИмяЗадачи			= КомандаПриложения.ЗначениеАргумента("TASK");
	АргументыЗадачи		= КомандаПриложения.ЗначениеАргумента("ARGS");
	ОпцииЗадачи			= ПреобразоватьМассивВСоответствие(КомандаПриложения.ЗначениеОпции("option"));
	ПеременныеОкружения	= ПреобразоватьМассивВСоответствие(КомандаПриложения.ЗначениеОпции("env"));
	ТипЗадачи			= КомандаПриложения.ЗначениеОпции("type");
	
	ФайлНастройкиЗадач 	= КомандаПриложения.ЗначениеОпции("tasks-file");
	
	РабочийКаталог 		= КомандаПриложения.ЗначениеОпции("work-dir");
	РабочаяОбласть		= КомандаПриложения.ЗначениеОпции("workspace");

	ЭтоСохранениеЗадачи = КомандаПриложения.ЗначениеОпции("save");
	ЭтоГруппаЗадач 		= КомандаПриложения.ЗначениеОпции("group");
	ПредставлениеЗадачи	= КомандаПриложения.ЗначениеОпции("label");
	НаименованиеЗадачи	= КомандаПриложения.ЗначениеОпции("name");
	ПерехватыватьПотоки	= КомандаПриложения.ЗначениеОпции("redirect-out");

	Лог = Логирование.ПолучитьЛог("oscript.app.tasks");
	ЦветнойВывод = Новый ЦветнойВыводЛогаКонсоль(Истина, Истина, Истина);
	Лог.ДобавитьСпособВывода(ЦветнойВывод);

	РежимОтладки = КомандаПриложения.ЗначениеОпции("verbose");
	МенеджерЗадач = Новый МенеджерЗадач;
	МенеджерЗадач.УстановитьОтладку(РежимОтладки);
	МенеджерЗадач.Настроить(РабочаяОбласть, РабочийКаталог, ФайлНастройкиЗадач);
	
	Если ЭтоСохранениеЗадачи Тогда
		МенеджерЗадач.СохранитьЗадачу(ИмяЗадачи, НаименованиеЗадачи, ПредставлениеЗадачи, ЭтоГруппаЗадач, ТипЗадачи, АргументыЗадачи, ОпцииЗадачи, ПеременныеОкружения, ПерехватыватьПотоки);
	Иначе
		МенеджерЗадач.ВыполнитьЗадачу(ИмяЗадачи, ТипЗадачи, АргументыЗадачи, ОпцииЗадачи, ПеременныеОкружения, ПерехватыватьПотоки);
	КонецЕсли;

КонецПроцедуры

Функция ПреобразоватьМассивВСоответствие(МассивПар)

	Соответствие = Новый Соответствие();

	Для каждого СтрокаМассива Из МассивПар Цикл
		
		МассивКлюча = СтрРазделить(СтрокаМассива, "=");

		Если МассивКлюча.Количество() = 2 Тогда
			Соответствие.Вставить(МассивКлюча[0], МассивКлюча[1]);
		Иначе
			Соответствие.Вставить(СтрокаМассива);
		КонецЕсли;

	КонецЦикла;
	
	Возврат Соответствие;

КонецФункции