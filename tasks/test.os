#Использовать 1bdd
#Использовать logos
#Использовать advanced-logos
#Использовать 1testrunner

Перем Лог;

Функция ПрогнатьТесты()
	
	Тестер = Новый Тестер;

	ПутьКТестам = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "tests");
	ПутьКОтчетуJUnit = ОбъединитьПути(ТекущийСценарий().Каталог, "..");

	КаталогТестов = Новый Файл(ПутьКТестам);

	
	Если Не КаталогТестов.Существует() Тогда
		Лог.Поля("КаталогТестов", КаталогТестов.ПолноеИмя).Информация("Не найден каталог тестов");
		Возврат Истина;
	КонецЕсли;

	МассивФайловТестов = НайтиФайлы(КаталогТестов, "*.os");
	Если МассивФайловТестов.Количество() = 0 Тогда
		Лог.Поля("КаталогТестов", КаталогТестов.ПолноеИмя).Информация("Не найдены файлы тестов");
		Возврат Истина;
	КонецЕсли;
	РезультатТестирования = Тестер.ТестироватьКаталог(
		КаталогТестов,
		Новый Файл(ПутьКОтчетуJUnit)
	);

	Успешно = РезультатТестирования = 0;
	
	Возврат Успешно;
КонецФункции // ПрогнатьТесты()

Функция ПрогнатьФичи()
	
	ПутьОтчетаJUnit = "./bdd-log.xml";

	КаталогФич = ОбъединитьПути(".", "features");

	Файл_КаталогФич = Новый Файл(КаталогФич);
	Если Не Файл_КаталогФич.Существует() Тогда
		Лог.Поля("КаталогФич", Файл_КаталогФич.ПолноеИмя).Информация("Не найден каталог");
		Возврат Истина;
	КонецЕсли;

	ИсполнительБДД = Новый ИсполнительБДД;
	РезультатыВыполнения = ИсполнительБДД.ВыполнитьФичу(Файл_КаталогФич, Файл_КаталогФич);
	ИтоговыйРезультатВыполнения = ИсполнительБДД.ПолучитьИтоговыйСтатусВыполнения(РезультатыВыполнения);

	СтатусВыполнения = ИсполнительБДД.ВозможныеСтатусыВыполнения().НеВыполнялся;
	Если РезультатыВыполнения.Строки.Количество() > 0 Тогда
		
		СтатусВыполнения = ИсполнительБДД.ПолучитьИтоговыйСтатусВыполнения(РезультатыВыполнения);
		
	КонецЕсли;

	ГенераторОтчетаJUnit = Новый ГенераторОтчетаJUnit;
	ГенераторОтчетаJUnit.Сформировать(РезультатыВыполнения, СтатусВыполнения, ПутьОтчетаJUnit);

	Лог.Поля("Результат", ИтоговыйРезультатВыполнения).Информация(СтрШаблон("Результат прогона фич <%1>", ИтоговыйРезультатВыполнения));

	Возврат ИтоговыйРезультатВыполнения <> ИсполнительБДД.ВозможныеСтатусыВыполнения().Сломался;
КонецФункции // ПрогнатьФичи()

Лог = Логирование.ПолучитьЛог("oscript.task").Поля("Префикс", "test");
ЦветнойВывод = Новый ЦветнойВыводЛогаКонсоль();
Лог.ДобавитьСпособВывода(ЦветнойВывод);

ТестыПрошли = Истина;

Попытка
	ТестыПрошли = ПрогнатьТесты();

Исключение
	ТестыПрошли = Ложь;
	Лог.Ошибка(СтрШаблон("Тесты через 1testrunner выполнены неудачно %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
КонецПопытки;

ФичиПрошли = Истина;

Попытка
	ФичиПрошли = ПрогнатьФичи();
Исключение
	ФичиПрошли = Ложь;
	Лог.Ошибка(СтрШаблон("Тесты поведения через 1bdd выполнены неудачно %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
КонецПопытки;

Если Не ТестыПрошли Или Не ФичиПрошли Тогда
	ВызватьИсключение "Тестирование завершилось неудачно!";
Иначе
	Лог.Информация(СтрШаблон("Результат прогона тестов <%1>", ТестыПрошли));
КонецЕсли;